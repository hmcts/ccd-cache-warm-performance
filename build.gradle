plugins {
  id 'org.owasp.dependencycheck' version '9.0.4'
  id 'scala'
  id 'java-library'
  id 'io.gatling.gradle' version '3.9.5.6'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaExec) {
  // Configure the size of the JVM if necessary
  jvmArgs = ['-Xms1024m', '-Xmx2048m']
  /* Use the following args to build a report when a test was terminated
  args = [
      "--reports-only", "fact-20201109092032896",
      '--results-folder', file('build/reports/gatling').absolutePath,
      ]
  */
}

tasks.withType(Copy).all{
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

repositories {
  mavenCentral()
}

dependencies {
  implementation project(":common-performance")
}

gatling {
  gatlingVersion '3.9.5'
  scalaVersion '2.13.10'
  simulations = {
    include "simulations/CacheWarm_Simulation.scala"
  }
}

sourceSets {
  gatling {
    scala {
      srcDir 'src/gatling/simulations'
    }
    resources {
      srcDir 'src/gatling/resources'
    }
  }
}

ext {
  transactionNamesToGraph = ["CCDCacheWarm_000_Auth", "CCDCacheWarm_000_LoadJurisdictions"]
}

task generateStats(type: JavaExec) {
  dependsOn gatlingRun
  dependsOn compileGatlingScala
  mainClass.set('stats.GenerateStatsByTxn')
  args = transactionNamesToGraph
  classpath = sourceSets.gatling.runtimeClasspath + project(":common-performance").sourceSets.main.runtimeClasspath
}

gatlingRun.finalizedBy generateStats